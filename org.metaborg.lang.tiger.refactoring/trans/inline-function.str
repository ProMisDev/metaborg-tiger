module inline-function

imports 
	signatures/-
	nabl2/api
	pp
	libspoofax/stratego/debug
	renaming

rules // menu action strategies
  inline-call-action :
    (selected-id, _, ast, path, project-path) -> (filename, result)
    where
    	analysis := <nabl2-get-ast-analysis> ast;
      <not(nabl2-analysis-has-errors)> analysis
    with
      filename := <guarantee-extension(|"refactored.tig")> path
      ; (params, body, target-dec-occ) := <exec-get-function(|analysis, selected-id)> ast
      ; result := <inline-this-function-call(|selected-id, body, params); pp-Tiger-string> ast

  inline-function-action :
    (selected-id, _, ast, path, project-path) -> (filename, result)
    where
    	analysis := <nabl2-get-ast-analysis> ast;
      <not(nabl2-analysis-has-errors)> analysis
    with
      filename := <guarantee-extension(|"refactored.tig")> path
      ; (params, body, target-dec-occ) := <exec-get-function(|analysis, selected-id)> ast
      ;	check-recursive(|analysis, target-dec-occ, body, params) 
      ; inlined-ast := <inline-all-calls(|analysis, target-dec-occ, body, params)> ast  
      ; cleaned-ast := <delete-fun-dec(|target-dec-occ)> inlined-ast      
      ; result := <pp-Tiger-string> cleaned-ast
       
rules  //inlining steps
  exec-get-function(|analysis, selected-id): ast -> (params, body, target-dec-occ)
    where
      target-dec-occ := <collect-one(get-function-declaration(|analysis, selected-id)) > ast
      ; (params, body) := <collect-one(get-function(|target-dec-occ))> ast
      
  inline-this-function-call(|selected-id, body, params): ast -> inlined-ast
    where
      inlined-ast := <oncetd(replace-this-call(|selected-id, body, params))> ast
      
  check-recursive(|analysis, target-dec-occ, body, params) = 
		<not(collect-one(replace-call(|analysis, target-dec-occ, body, params)))>  body
		
	inline-all-calls(|analysis, target-dec-occ, body, params): ast -> inlined-ast
		where
			inlined-ast := <topdown(try(replace-call(|analysis, target-dec-occ, body, params)))>
			
	delete-fun-dec(|target-dec-occ): ast -> cleaned-ast-2
		where
			cleaned-ast := <topdown(try(delete-function(|target-dec-occ)))> ast
			; cleaned-ast-2 := <topdown(try(delete-empty-fun-decs))> cleaned-ast
      
rules //get-function-declaration
	get-function-declaration(|analysis, selected-id): Call(name, args) -> dec-occ
	where
	  dec-occ := <get-dec-from-ref(|analysis, "Var", name, name)> selected-id
	  
rules //get-function
  get-function(|target-dec-occ): FunDec(name, params, type, body) -> (params, body)
	where
		check-dec-occ(|"Var", name, target-dec-occ)
		
  get-function(|target-dec-occ): ProcDec(name, params, body) -> (params, body)
	where
		check-dec-occ(|"Var", name, target-dec-occ)
		
rules //replace-this-call
  replace-this-call(|select-id, body, params): Call(name, args) -> Let(vars, [body])
  where
  	<?select-id> name
  	; vars := <zip(create-var-dec)> (params, args)
  	
rules //replace-call
  replace-call(|analysis, target-dec-occ, body, params): Call(name, args) -> Let(vars, [body])
  where
  	check-ref-occ(|analysis, "Var", name, target-dec-occ)
  	; vars := <zip(create-var-dec)> (params, args)
  	
rules //delete-function-dec
	delete-function(|target-dec-occ): FunDecs(funDecs) -> FunDecs(reducedFunDecs)
	where
	  reducedFunDecs := <filter(delete-function-dec(|target-dec-occ))> funDecs
	    
  delete-function-dec(|target-dec-occ): fun@FunDec(name, params, type, body) -> fun
  where
    not(check-dec-occ(|"Var", name, target-dec-occ))
    
  delete-function-dec(|target-dec-occ): proc@ProcDec(name, params, body) -> proc
  where
    not(check-dec-occ(|"Var", name, target-dec-occ))
    
  delete-empty-fun-decs: Let(decs, exps) -> Let(cleanedDecs, exps)
  where
  	cleanedDecs := <filter(not(?FunDecs([])))> decs
  
rules //create-var-dec
	create-var-dec : (FArg(name, tid), exp) -> VarDec(name, tid, exp)

		
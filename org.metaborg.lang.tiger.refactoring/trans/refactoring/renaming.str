module renaming

imports 
	signatures/-
	nabl2/api
	nabl2/shared/-
	pp
	libspoofax/stratego/debug
	refactoring/refactoring-util


rules // menu action strategies
  rename-action :
    (selected-term, _, ast, path, project-path) -> (filename, result)
    where
    	analysis := <nabl2-get-ast-analysis> ast;
      <not(nabl2-analysis-has-errors)> analysis
    with
    <log-error-on-fail(not(eq)|"ERROR: No occurrence selected ")> (selected-term, ast)
		; (occs, decs, refs) := <collect-occs(|analysis)> ast
    ; target-dec-occ := <get-target-dec-occ(|analysis, occs, decs)> selected-term 
    ; target-indices := <get-target-indices(|analysis, refs)> target-dec-occ
		; new-name := <read-config-file> ($[[project-path]/[path].rfac], "refactoring.rename")
		; old-name-bindings := <get-all-name-bindings(|analysis)> ast
		; renamed-ast := <bottomup(try(rename(|target-indices, new-name)))> ast
    ; check-capture(|renamed-ast, old-name-bindings, path)
    ; filename := <guarantee-extension(|"refactored.tig")> path
    ; result := <pp-Tiger-string> renamed-ast
    
rules // renaming steps
	collect-occs(|analysis): ast -> (occs, decs, refs)
	where
		decs := <nabl2-get-all-decls(|analysis)> ast
    ; refs  := <nabl2-get-all-refs(|analysis)> ast
    ; occs := <conc> (decs, refs)
    
	get-target-dec-occ(|analysis, occs, decs): selected-term -> target-dec-occ
	where
		selected-occ := <collect-one(get-occurrence(|occs))> selected-term
    ; target-dec-occ := <resolve-selected-occ(|analysis, decs)> selected-occ
    
  get-target-indices(|analysis, refs): target-dec-occ -> target-indices
  where
  	target-ref-occs := <filter(resolves-to-dec(|analysis, target-dec-occ))> refs
    ; target-occs := <add-elem> (target-dec-occ, target-ref-occs)
    ; target-indices := <map(get-term-index-from-occ)> target-occs

  rename(|target-indices, new-name): t -> new-name
	where
		<is-string> t
	 	; term-index := <nabl2-get-ast-index> t
	  ; <elem> (term-index, target-indices)
	  
  check-capture(|renamed-ast, old-name-bindings, path) =
		(new-renamed-ast, new-analysis, _, _, _) := <nabl2-analyze-ast(|path)> renamed-ast
		; new-name-bindings := <get-all-name-bindings(|new-analysis)> new-renamed-ast
		; old-name-binding-indexes := <get-name-binding-indexes> old-name-bindings
		; new-name-binding-indexes := <get-name-binding-indexes> new-name-bindings
		; <log-error-on-fail(eq|"ERROR: Capture detected ")> (old-name-binding-indexes, new-name-binding-indexes)
    
rules
	get-occurrence(|occs): t -> occurrence
	where
		<is-string> t
		; term-index := <nabl2-get-ast-index> t
		; occurrence := <fetch-elem(occ-eq-term-index(|term-index))> occs
		
	occ-eq-term-index(|target-term-index): occ@Occurrence(_, _, term-index) -> occ
	where
		<?target-term-index> term-index
		
  resolve-selected-occ(|analysis, decs): selected-occ -> target-dec-occ
  where
  	if <elem> (selected-occ, decs) then
  		target-dec-occ := selected-occ
  	else
  		(target-dec-occ, _) := <nabl2-get-resolved-name(|analysis)> selected-occ
  	end
  	
  resolves-to-dec(|analysis, target-dec-occ): ref-occ -> ref-occ
  where
  	(dec-occ, _) := <nabl2-get-resolved-name(|analysis)> ref-occ
  	; <?target-dec-occ> dec-occ
		
	get-term-index-from-occ: Occurrence(_, _, term-index) -> term-index
	 	
module renaming-test

imports 
	signatures/-
	nabl2/api
	pp
	libspoofax/stratego/debug
	renaming
	

rules // menu action strategies
  rename-test-var-no-type : ast -> renamed-ast
    where
    	analysis := <nabl2-get-ast-analysis> ast
      ; <not(nabl2-analysis-has-errors)> analysis
    with
      old-name := "msg"
      ; new-name := "message"
      ; path := "test/renaming.spt"
      ; target-dec := VarDecNoType(old-name, Nil())
    	; target-dec-occ := <collect-one(get-declaration-test(|old-name, "Var"))> ast
    	; (old-occ-count, renamed-ast) := <exec-rename(|analysis, target-dec-occ, new-name)> ast
    	; check_capture(|renamed-ast, target-dec-occ, new-name, old-occ-count, path)
    	
  rename-test-var-capture : ast -> renamed-ast
    where
    	analysis := <nabl2-get-ast-analysis> ast
      ; <not(nabl2-analysis-has-errors)> analysis
    with
      old-name := "y"
      ; new-name := "x"
      ; path := "test/renaming.spt"
      ; target-dec := VarDecNoType(old-name, Nil())
    	; target-dec-occ := <collect-one(get-declaration-test(|old-name, "Var"))> ast
    	; (old-occ-count, renamed-ast) := <exec-rename(|analysis, target-dec-occ, new-name)> ast
    	; not(check_capture(|renamed-ast, target-dec-occ, new-name, old-occ-count, path))
    
rules
	exec-rename(|analysis, target-dec-occ, new-name): ast -> (old-occ-count, renamed-ast)
	 where
	    old-occ-count := <occurrences(rename(|analysis, target-dec-occ, new-name))> ast
    	; renamed-ast := <topdown(try(rename(|analysis, target-dec-occ, new-name)))> ast
    	
rules
  check_capture(|renamed-ast, target-dec-occ, new-name, old-occ-count, path) =
  		(new-renamed-ast, new-analysis, _, _, _) := <nabl2-analyze-ast(|path)> renamed-ast
		  ; indexed-target-name := <nabl2-get-occurrence-name> target-dec-occ
		  ; new-target-name := <nabl2-copy-ast-index(|indexed-target-name)> new-name
		  ; target-namespace := <nabl2-get-occurrence-ns> target-dec-occ
		  ; new-target-dec-occ := <nabl2-mk-occurrence(|target-namespace)> new-target-name
		  ; new-ref-count := <occurrences(rename(|new-analysis, new-target-dec-occ, "_invalid"))> new-renamed-ast
		  ; <?old-occ-count> new-ref-count 
		  
rules
	get-declaration-test(|target-name, namespace): VarDecNoType(name, exp) -> dec-occ
	where
		<eq> (<strip-annos> target-name, <strip-annos> name) 
    ; dec-occ := <nabl2-mk-occurrence(|namespace)> name
    
  get-declaration-test(|target-name, namespace): VarDec(name, type, exp) -> dec-occ
	where
		<eq> (<strip-annos> target-name, <strip-annos> name) 
    ; dec-occ := <nabl2-mk-occurrence(|namespace)> name
    